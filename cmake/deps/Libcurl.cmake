if(WIN32)
    set(CMAKE_USE_OPENSSL OFF CACHE BOOL "" FORCE)
    set(CURL_USE_OPENSSL OFF CACHE BOOL "" FORCE)
    set(CMAKE_USE_MBEDTLS OFF CACHE BOOL "" FORCE)
    set(CURL_USE_MBEDTLS OFF CACHE BOOL "" FORCE)
    set(CMAKE_USE_SCHANNEL ON CACHE BOOL "" FORCE)
    set(CURL_USE_SCHANNEL ON CACHE BOOL "" FORCE)
    set(USE_OPENSSL OFF CACHE BOOL "" FORCE)
    set(USE_MBEDTLS OFF CACHE BOOL "" FORCE)
else()
    set(CMAKE_USE_OPENSSL OFF CACHE BOOL "" FORCE)
    set(CURL_USE_OPENSSL OFF CACHE BOOL "" FORCE)
    set(CMAKE_USE_MBEDTLS ON CACHE BOOL "" FORCE)
    set(CURL_USE_MBEDTLS ON CACHE BOOL "" FORCE)
    set(CMAKE_USE_SCHANNEL OFF CACHE BOOL "" FORCE)
    set(CURL_USE_SCHANNEL OFF CACHE BOOL "" FORCE)
    set(USE_OPENSSL OFF CACHE BOOL "" FORCE)
    set(USE_MBEDTLS ON CACHE BOOL "" FORCE)
endif()
set(CURL_ZLIB ON CACHE BOOL "" FORCE)
set(CURL_DISABLE_LDAP ON CACHE BOOL "" FORCE)
set(ENABLE_THREADED_RESOLVER ON CACHE BOOL "" FORCE)
set(BUILD_CURL_EXE OFF CACHE BOOL "" FORCE)
set(CURL_ENABLE_SSL ON CACHE BOOL "" FORCE)
set(CURL_USE_LIBSSH2 OFF CACHE BOOL "" FORCE)
set(CURL_BROTLI OFF CACHE STRING "" FORCE)
set(CURL_ZSTD ON CACHE STRING "" FORCE)
set(USE_NGHTTP2 OFF CACHE BOOL "" FORCE)
set(USE_LIBIDN2 OFF CACHE BOOL "" FORCE)
set(CURL_USE_LIBPSL OFF CACHE BOOL "" FORCE)
set(CURL_DISABLE_LDAP ON CACHE BOOL "" FORCE)
set(CURL_DISABLE_MQTT ON CACHE BOOL "" FORCE)
set(CURL_DISABLE_RTSP ON CACHE BOOL "" FORCE)
set(CURL_DISABLE_TELNET ON CACHE BOOL "" FORCE)
set(CURL_DISABLE_TFTP ON CACHE BOOL "" FORCE)
set(CURL_DISABLE_POP3 ON CACHE BOOL "" FORCE)
set(CURL_DISABLE_IMAP ON CACHE BOOL "" FORCE)
set(CURL_DISABLE_SMTP ON CACHE BOOL "" FORCE)
set(CURL_DISABLE_GOPHER ON CACHE BOOL "" FORCE)
set(CURL_DISABLE_RTMP ON CACHE BOOL "" FORCE)

set(CMAKE_DISABLE_FIND_PACKAGE_PkgConfig ON)

# Pre-configure zstd detection for libcurl - disable pkg-config path by
# pre-setting ZSTD_INCLUDE_DIR/ZSTD_LIBRARY so FindZstd.cmake skips pkg-config
# and parses the header file directly for version info
if(DEFINED ZSTD_INCLUDE_DIR AND DEFINED ZSTD_LIBRARY)
    # Unset any pkg-config results that might interfere
    unset(ZSTD_FOUND CACHE)
    unset(Zstd_FOUND CACHE)
    unset(ZSTD_VERSION CACHE)
    unset(Zstd_VERSION CACHE)
endif()

set(_envy_zlib_real_target "")
if(TARGET ZLIB::ZLIB)
    get_target_property(_envy_zlib_real_target ZLIB::ZLIB INTERFACE_LINK_LIBRARIES)
    if(_envy_zlib_real_target AND TARGET ${_envy_zlib_real_target})
        set(ZLIB_LIBRARY_RELEASE "$<TARGET_FILE:${_envy_zlib_real_target}>" CACHE STRING "" FORCE)
        set(ZLIB_LIBRARY_DEBUG "$<TARGET_FILE:${_envy_zlib_real_target}>" CACHE STRING "" FORCE)
    endif()
endif()

cmake_path(APPEND ENVY_CACHE_DIR "${ENVY_LIBCURL_ARCHIVE}" OUTPUT_VARIABLE _curl_archive)
set(_curl_url "${ENVY_LIBCURL_URL}")
if(EXISTS "${_curl_archive}")
    file(TO_CMAKE_PATH "${_curl_archive}" _curl_archive_norm)
    set(_curl_url "file://${_curl_archive_norm}")
endif()

cmake_path(APPEND ENVY_CACHE_DIR "libcurl-src" OUTPUT_VARIABLE libcurl_SOURCE_DIR)
cmake_path(APPEND CMAKE_BINARY_DIR "_deps" "libcurl-build" OUTPUT_VARIABLE libcurl_BINARY_DIR)

if(NOT EXISTS "${libcurl_SOURCE_DIR}/CMakeLists.txt")
    FetchContent_Populate(libcurl
        SOURCE_DIR "${libcurl_SOURCE_DIR}"
        BINARY_DIR "${libcurl_BINARY_DIR}"
        URL ${_curl_url}
        URL_HASH SHA256=${ENVY_LIBCURL_SHA256}
    )
endif()

if(DEFINED libcurl_SOURCE_DIR AND DEFINED libcurl_BINARY_DIR AND _envy_zlib_real_target)
    envy_patch_libcurl_cmakelists("${libcurl_SOURCE_DIR}" "${libcurl_BINARY_DIR}" "${_envy_zlib_real_target}")
endif()

add_subdirectory(${libcurl_SOURCE_DIR} ${libcurl_BINARY_DIR})
FetchContent_GetProperties(libcurl)
unset(_curl_archive)
unset(_curl_archive_norm)
unset(_curl_url)
if(NOT TARGET CURL::libcurl)
    add_library(CURL::libcurl ALIAS libcurl)
endif()
if(TARGET libcurl_static)
    if(DEFINED _libssh2_primary_target)
        add_dependencies(libcurl_static ${_libssh2_primary_target})
    endif()
    set_target_properties(libcurl_static PROPERTIES INTERFACE_LINK_LIBRARIES "")
    # Ensure libcurl sees the same mbedTLS config as mbedTLS library itself
    if(DEFINED MBEDTLS_USER_CONFIG_FILE)
        target_compile_definitions(libcurl_static PRIVATE
            MBEDTLS_USER_CONFIG_FILE="${MBEDTLS_USER_CONFIG_FILE}")
    endif()
endif()
if(TARGET ZLIB::ZLIB)
    if(NOT CMAKE_VERSION VERSION_LESS "3.21")
        set_property(TARGET libcurl_static APPEND PROPERTY INTERFACE_LINK_LIBRARIES ZLIB::ZLIB)
    else()
        target_link_libraries(libcurl_static INTERFACE ZLIB::ZLIB)
    endif()
endif()
if(TARGET libcurl_shared)
    if(DEFINED _libssh2_primary_target)
        add_dependencies(libcurl_shared ${_libssh2_primary_target})
    endif()
    set_target_properties(libcurl_shared PROPERTIES INTERFACE_LINK_LIBRARIES "")
endif()
set(CURL_INCLUDE_DIR "${libcurl_SOURCE_DIR}/include" CACHE PATH "" FORCE)
set(_envy_libcurl_target libcurl)
if(TARGET libcurl_static)
    set(_envy_libcurl_target libcurl_static)
endif()

unset(CURL_LIBRARY CACHE)
unset(CURL_LIBRARIES CACHE)

set(CURL_LIBRARY "$<TARGET_FILE:${_envy_libcurl_target}>" CACHE STRING "" FORCE)
set(CURL_LIBRARIES "${CURL_LIBRARY}" CACHE STRING "" FORCE)
set(ENVY_LIBCURL_INCLUDE "${libcurl_SOURCE_DIR}/include")
set(ENVY_LIBCURL_BINARY_INCLUDE "${libcurl_BINARY_DIR}/include")
unset(_libssh2_primary_target)
unset(_envy_libcurl_output_name)
unset(_envy_libcurl_archive_name)
unset(_envy_libcurl_prefix)
unset(_envy_libcurl_suffix)
unset(_envy_prefix_len)
unset(_envy_suffix_len)
unset(_envy_name_len)
unset(_envy_suffix_start)
unset(_envy_libcurl_target)
unset(_envy_zlib_real_target)
unset(CMAKE_DISABLE_FIND_PACKAGE_PkgConfig)
