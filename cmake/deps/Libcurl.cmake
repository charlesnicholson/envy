set(CMAKE_USE_OPENSSL ON CACHE BOOL "" FORCE)
set(CURL_USE_OPENSSL ON CACHE BOOL "" FORCE)
set(CURL_USE_SECTRANSP OFF CACHE BOOL "" FORCE)
set(CURL_ZLIB ON CACHE BOOL "" FORCE)
set(CURL_DISABLE_LDAP ON CACHE BOOL "" FORCE)
set(ENABLE_THREADED_RESOLVER ON CACHE BOOL "" FORCE)
set(BUILD_CURL_EXE OFF CACHE BOOL "" FORCE)
set(CURL_ENABLE_SSL ON CACHE BOOL "" FORCE)
set(CURL_BROTLI OFF CACHE STRING "" FORCE)
set(CURL_ZSTD OFF CACHE STRING "" FORCE)
set(USE_NGHTTP2 OFF CACHE BOOL "" FORCE)
set(USE_LIBIDN2 OFF CACHE BOOL "" FORCE)
set(CURL_USE_LIBPSL OFF CACHE BOOL "" FORCE)

set(_curl_ca_bundle "")
if(APPLE)
    set(_curl_ca_bundle "/etc/ssl/cert.pem")
elseif(CMAKE_SYSTEM_NAME STREQUAL "Linux")
    set(_curl_ca_bundle "/etc/ssl/certs/ca-certificates.crt")
endif()

if(_curl_ca_bundle AND EXISTS "${_curl_ca_bundle}")
    set(CURL_CA_BUNDLE "${_curl_ca_bundle}" CACHE STRING "" FORCE)
else()
    set(CURL_CA_BUNDLE "auto" CACHE STRING "" FORCE)
endif()
unset(_curl_ca_bundle)
set(CMAKE_DISABLE_FIND_PACKAGE_PkgConfig ON)
cmake_path(APPEND CODEX_THIRDPARTY_CACHE_DIR "${CODEX_LIBCURL_ARCHIVE}" OUTPUT_VARIABLE _curl_archive)
set(_curl_url "${CODEX_LIBCURL_URL}")
if(EXISTS "${_curl_archive}")
    file(TO_CMAKE_PATH "${_curl_archive}" _curl_archive_norm)
    set(_curl_url "file://${_curl_archive_norm}")
endif()

FetchContent_Declare(libcurl
    URL ${_curl_url}
    URL_HASH SHA256=${CODEX_LIBCURL_SHA256}
)
FetchContent_GetProperties(libcurl)
if(NOT libcurl_POPULATED)
    FetchContent_Populate(libcurl)
    FetchContent_GetProperties(libcurl)
endif()
add_subdirectory(${libcurl_SOURCE_DIR} ${libcurl_BINARY_DIR})
unset(_curl_archive)
unset(_curl_archive_norm)
unset(_curl_url)
if(NOT TARGET CURL::libcurl)
    add_library(CURL::libcurl ALIAS libcurl)
endif()
if(TARGET libcurl_static)
    add_dependencies(libcurl_static openssl)
    if(DEFINED _libssh2_primary_target)
        add_dependencies(libcurl_static ${_libssh2_primary_target})
    endif()
    set_target_properties(libcurl_static PROPERTIES INTERFACE_LINK_LIBRARIES "")
endif()
if(TARGET libcurl_shared)
    add_dependencies(libcurl_shared openssl)
    if(DEFINED _libssh2_primary_target)
        add_dependencies(libcurl_shared ${_libssh2_primary_target})
    endif()
    set_target_properties(libcurl_shared PROPERTIES INTERFACE_LINK_LIBRARIES "")
endif()
set(CURL_INCLUDE_DIR "${libcurl_SOURCE_DIR}/include" CACHE PATH "" FORCE)
set(CURL_LIBRARY "${libcurl_BINARY_DIR}/lib/libcurl.a" CACHE FILEPATH "" FORCE)
set(CURL_LIBRARIES "${CURL_LIBRARY}" CACHE STRING "" FORCE)
unset(_libssh2_primary_target)
unset(CMAKE_DISABLE_FIND_PACKAGE_PkgConfig)
