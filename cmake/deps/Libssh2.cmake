if(WIN32)
    set(LIBSSH2_WITH_MBEDTLS OFF CACHE BOOL "" FORCE)
    set(LIBSSH2_WITH_WINCNG ON CACHE BOOL "" FORCE)
    set(CRYPTO_BACKEND WinCNG CACHE STRING "" FORCE)
else()
    set(LIBSSH2_WITH_MBEDTLS ON CACHE BOOL "" FORCE)
    set(LIBSSH2_WITH_WINCNG OFF CACHE BOOL "" FORCE)
    set(CRYPTO_BACKEND mbedTLS CACHE STRING "" FORCE)
endif()
set(LIBSSH2_WITH_OPENSSL OFF CACHE BOOL "" FORCE)
set(LIBSSH2_WITH_LIBGCRYPT OFF CACHE BOOL "" FORCE)
set(ENABLE_ZLIB_COMPRESSION ON CACHE BOOL "" FORCE)
set(LIBSSH2_BUILD_TESTING OFF CACHE BOOL "" FORCE)
set(BUILD_EXAMPLES OFF CACHE BOOL "" FORCE)
set(BUILD_STATIC_LIBS ON CACHE BOOL "" FORCE)
set(BUILD_SHARED_LIBS OFF CACHE BOOL "" FORCE)
cmake_path(APPEND ENVY_CACHE_DIR "${ENVY_LIBSSH2_ARCHIVE}" OUTPUT_VARIABLE _libssh2_archive)
set(_libssh2_url "${ENVY_LIBSSH2_URL}")
if(EXISTS "${_libssh2_archive}")
    file(TO_CMAKE_PATH "${_libssh2_archive}" _libssh2_archive_norm)
    set(_libssh2_url "file://${_libssh2_archive_norm}")
endif()

cmake_path(APPEND ENVY_CACHE_DIR "libssh2-src" OUTPUT_VARIABLE libssh2_SOURCE_DIR)
cmake_path(APPEND CMAKE_BINARY_DIR "_deps" "libssh2-build" OUTPUT_VARIABLE libssh2_BINARY_DIR)

if(NOT EXISTS "${libssh2_SOURCE_DIR}/CMakeLists.txt")
    FetchContent_Populate(libssh2
        SOURCE_DIR "${libssh2_SOURCE_DIR}"
        BINARY_DIR "${libssh2_BINARY_DIR}"
        URL ${_libssh2_url}
        URL_HASH SHA256=${ENVY_LIBSSH2_SHA256}
    )
endif()

if(DEFINED libssh2_SOURCE_DIR AND DEFINED libssh2_BINARY_DIR)
    envy_patch_libssh2("${libssh2_SOURCE_DIR}" "${libssh2_BINARY_DIR}")
    envy_patch_libssh2_install("${libssh2_SOURCE_DIR}" "${libssh2_BINARY_DIR}")
endif()

add_subdirectory(${libssh2_SOURCE_DIR} ${libssh2_BINARY_DIR})

unset(_libssh2_archive)
unset(_libssh2_archive_norm)
unset(_libssh2_url)
if(TARGET libssh2::libssh2)
    set(_libssh2_primary_target libssh2::libssh2)
elseif(TARGET libssh2_static)
    set(_libssh2_primary_target libssh2_static)
elseif(TARGET libssh2)
    set(_libssh2_primary_target libssh2)
else()
    message(FATAL_ERROR "libssh2 target was not created by FetchContent")
endif()

if(NOT TARGET libssh2::libssh2)
    add_library(libssh2::libssh2 ALIAS ${_libssh2_primary_target})
    set_target_properties(${_libssh2_primary_target} PROPERTIES INTERFACE_LINK_LIBRARIES "")
endif()

set(Libssh2_DIR "${libssh2_BINARY_DIR}" CACHE PATH "" FORCE)
set(LIBSSH2_DIR "${libssh2_BINARY_DIR}" CACHE PATH "" FORCE)
set(LIBSSH2_INCLUDE_DIR "${libssh2_SOURCE_DIR}/include" CACHE PATH "" FORCE)
set(LIBSSH2_INCLUDE_DIRS "${libssh2_SOURCE_DIR}/include" CACHE PATH "" FORCE)

unset(LIBSSH2_LIBRARY CACHE)
unset(LIBSSH2_LIBRARIES CACHE)

set(LIBSSH2_LIBRARY "$<TARGET_FILE:${_libssh2_primary_target}>" CACHE STRING "" FORCE)
set(LIBSSH2_LIBRARIES "${LIBSSH2_LIBRARY}" CACHE STRING "" FORCE)
set(LIBSSH2_LIBRARY_DIR "${libssh2_BINARY_DIR}/src" CACHE PATH "" FORCE)

set(_envy_libssh2_actual "${_libssh2_primary_target}")
if(_envy_libssh2_actual STREQUAL "libssh2::libssh2")
    get_target_property(_envy_libssh2_actual libssh2::libssh2 ALIASED_TARGET)
endif()
if(_envy_libssh2_actual AND TARGET ${_envy_libssh2_actual})
    target_compile_definitions(${_envy_libssh2_actual} PRIVATE __STDC_WANT_LIB_EXT1__=1)
    # Ensure libssh2 sees the same mbedTLS config as mbedTLS library itself (non-Windows only)
    if(NOT WIN32 AND DEFINED MBEDTLS_USER_CONFIG_FILE)
        target_compile_definitions(${_envy_libssh2_actual} PRIVATE
            MBEDTLS_USER_CONFIG_FILE="${MBEDTLS_USER_CONFIG_FILE}")
    endif()
    if(CMAKE_SYSTEM_NAME STREQUAL "Linux")
        target_compile_definitions(${_envy_libssh2_actual} PRIVATE _DEFAULT_SOURCE)
    endif()
    if(DEFINED envy_zlib_SOURCE_DIR AND DEFINED envy_zlib_BINARY_DIR)
        target_include_directories(${_envy_libssh2_actual} PRIVATE
            "${envy_zlib_SOURCE_DIR}"
            "${envy_zlib_BINARY_DIR}")
    endif()
    if(MSVC)
        target_compile_options(${_envy_libssh2_actual} PRIVATE /wd4200 /wd4127)
    elseif(CMAKE_C_COMPILER_ID MATCHES "Clang|GNU")
        target_compile_options(${_envy_libssh2_actual} PRIVATE -Wno-unused-macros)
    endif()
endif()
unset(_envy_libssh2_actual)

set(ENV{PKG_CONFIG_PATH} "")
